defmodule Servy.SensorServer do

  @name :sensor_server

  use GenServer

  defmodule State do
    defstruct sensor_data: %{},
              refresh_interval: :timer.minutes(60)
  end

  # Client Interface

  def start_link(interval) do
    IO.puts("Starting the sensor server with #{interval} min refresh...")
    GenServer.start_link(__MODULE__, %State{ refresh_interval: :timer.minutes(interval)}, name: @name)
  end

  def get_sensor_data do
    GenServer.call @name, :get_sensor_data
  end

  def set_refresh_interval(milliseconds) do
    GenServer.cast @name, {:set_refresh_interval, milliseconds}
  end

  # Server Callbacks

  def init(state) do
    initial_sensor_data = run_tasks_to_get_sensor_data()
    schedule_refresh(state.refresh_interval)
    {:ok, %State{state | sensor_data: initial_sensor_data}}
  end

  def handle_info(:refresh, state) do
    IO.puts("Refreshing the cache...")
    new_sensor_data = run_tasks_to_get_sensor_data()
    schedule_refresh(state.refresh_interval)
    {:noreply, %State{state | sensor_data: new_sensor_data}}
  end

  def handle_info(unexpected, state) do
    IO.puts("Can't touch this! #{inspect(unexpected)}")
    {:noreply, state}
  end

  defp schedule_refresh(interval) do
    Process.send_after(self(), :refresh, interval)
  end

  def handle_call(:get_sensor_data, _from, state) do
    {:reply, state.sensor_data, state}
  end

  def handle_cast({:set_refresh_interval, milliseconds}, state) do
    schedule_refresh(milliseconds)
    {:noreply, %State{state | refresh_interval: milliseconds}}
  end

  defp run_tasks_to_get_sensor_data do
    IO.puts "Running tasks to get sensor data..."

    task = Task.async(fn -> Servy.Tracker.get_location("bigfoot") end)

    snapshots =
      ["cam-1", "cam-2", "cam-3"]
      |> Enum.map(&Task.async(fn -> Servy.VideoCam.get_snapshot(&1) end))
      |> Enum.map(&Task.await/1)

    where_is_bigfoot = Task.await(task)

    %{snapshots: snapshots, location: where_is_bigfoot}
  end


end
